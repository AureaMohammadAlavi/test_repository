buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "distribution"
apply plugin: "signing"


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'commons-io:commons-io:2.6'
}


group = "me.smash.gradle"
version = new ProjectVersion(1, 0, System.env.BUILD_NUMBER)

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

artifacts {
    archives sourceJar
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


bintray {
    user = 'aureamohammadalavi'
    key = System.getenv("BINTRAY_API_KEY")
    pkg {
        repo = "smash-repo"
        name = "my-first-package"
        version {
            name = project.version
        }
    }
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact distZip
            pom {
                description = "a simple library to test drive artifact publishing"
                developers {
                    developer {
                        id = "smash1986"
                        name = "Mohammad Alavi"
                        email = "m.alavi1986@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "smash"
            url = "$rootDir/../smash-repo"
        }

        maven {
            name = "bintary"
            url = "https://api.bintray.com/maven/aureamohammadalavi/smash-repo/my-first-package/;publish=1"

            credentials {
                username = "aureamohammadalavi"
                password = System.getenv("BINTRAY_API_KEY")
            }
        }
    }
}

class ProjectVersion {
    int major
    int minor
    String build

    ProjectVersion(int major, int minor, String build) {
        this.major = major
        this.minor = minor
        this.build = build
    }


    @Override
    String toString() {
        if (build == null) {
            return "$major.$minor"
        } else {
            return "$major.$minor.$build"
        }
    }
}

jar {
    doFirst {
        Properties properties = new Properties()
        properties.version = project.version.toString()
        def file = file("$buildDir/resources/main/version.properties")
        file.parentFile.mkdirs()
        file.withOutputStream {
            properties.store(it, null)
        }
    }
}


distributions {
    main {
        contents {
            from {
                libsDir
            }
        }
    }
}

signing {
    sign publishing.publications.MyPublication
}

task runJar(type:JavaExec) {
    classpath = sourceSets.main.compileClasspath + jar.outputs.files
    main = "me.smash.gradle.Sample"
    println classpath
}

gradle.taskGraph.whenReady {
    ext."signing.password" = System.getenv("SIGNING_PASSWORD")
}
distZip.dependsOn jar, sourceJar
